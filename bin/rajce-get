#!/usr/bin/perl
# ABSTRACT: Download images from rajce.net.
# PODNAME: rajce-get

use warnings;
use strict;
use Getopt::Std;
use WWW::Mechanize;
use File::Basename;
use File::Path 2.06_05 qw(make_path);
use Pod::Usage;
use POSIX;
use WebService::Rajce;

my $url_pattern="^http:\/\/(.*)\.rajce\.idnes\.cz\/(.*)";

$|++;

my %opt;
getopts('Vhvu:s', \%opt);

my $version = $WebService::Rajce::VERSION;

if($opt{h}){
	pod2usage(-verbose => 2, -output => '-');
	exit;
}

if($opt{V}){
	print "$version\n";
	exit;
}

if(!$opt{u} or $opt{u} !~ /$url_pattern/ or $opt{h}){
	pod2usage(-verbose => 1, -output => '-');
	exit;
}

my ($username,$album) = $opt{u} =~ $url_pattern;

my $url = "http://$username.rajce.idnes.cz/$album";

my $bot = WWW::Mechanize->new(autocheck => 1, agent => 'rajce-get/'.$version);
$bot->env_proxy();
$bot->add_header('Accept-Encoding'=>'text/html');
$bot->cookie_jar(HTTP::Cookies->new());

my $gal = $bot->get($url);

my $storageurl = $gal->content();
$storageurl =~ s/.*var storage = "([^"]*)";.*/$1/s;

my @images = $bot->find_all_links(tag => "a", url_regex => qr/$storageurl/ );

my $outdir = "$username/$album";

info("Creating directory $outdir\n",%opt);
make_path($outdir);

# required number of base-10 digits for output name prefix
my $prefixlen = ceil(log(scalar @images)/log(10));
my $outidx = 0;
foreach my $imglink (@images){
	my $outname = basename($imglink->url());
	if($opt{s}){
		$outname = sprintf("%0".$prefixlen."d", $outidx)."_".$outname;
	}
	info("Downloding ".basename($imglink->url())." ...",%opt);
	$bot->get($imglink->url, ':content_file' => "$outdir/".$outname);
	$outidx = $outidx + 1;
	sleep 1;
	info(" OK\n",%opt);
}

sub info{
	my ($message,%opt) = @_;
	if($opt{v}){
		print "$message";
	}
}
__END__

=head1 SYNOPSIS

rajce-get [-V ] [-h] [-v] [-s] -u URL

=head1 OPTIONS AND ARGUMENTS

	 -h - Help
	 -v - Verbose mode
	 -s - Sort output file names in the same order as they appear in album
	 -u http://username.rajce.idnes.cz/album - Address of requested album
	 -V - Show version and exit

=head1 DESCRIPTION

This program will download images from rajce.net. Images are saved in
directory username/album

Behind proxy server try:

export http_proxy=http://login:password@proxyserver:port

=head1 SEE ALSO

https://metacpan.org/module/WebService::Rajce

=cut
